
;; 2.3.1 - Quotation
(define (memq item x)
  (cond ((null? x) false)
        ((eq? item (car x)) x)
        (else (memq item (cdr x)))))

;; Ex 2.54
(define (equal? l1 l2)
  (cond ((symbol? l1) (eq? l1 l2))
        ((list? l1) (and (equal? (car l1) (car l2))
                         (equal? (cdr l1) (cdr l2))))))

;; 2.3.2 - Symbolic Differentiation
;; Ex 2.56
;; Ex 2.57

(define (eqnumber? exp num)
  (and (number? exp) (= exp num)))
(define (accumulate op initial sequence)
  (if (null? sequence)
      initial
      (op (car sequence)
          (accumulate op initial (cdr sequence)))))

(define (variable? x) (symbol? x))
(define (same-variable? v1 v2)
  (and (variable? v1) (variable? v2) (eq? v1 v2)))
(define (make-sum a1 a2)
  (cond ((eqnumber? a1 0) a2)
        ((eqnumber? a2 0) a1)
        ((and (number? a1) (number? a2)) (+ a1 a2))
        (else (list '+ a1 a2))))
(define (make-product m1 m2)
  (cond ((or (eqnumber? m1 0) (eqnumber? m2 0)) 0)
        ((eqnumber? m1 1) m2)
        ((eqnumber? m2 1) m1)
        ((and (number? m1) (number? m2)) (* m1 m2))
        (else (list '* m1 m2))))
(define (make-exponent base exp)
  (cond ((eqnumber? exp 0) 1)
        ((eqnumber? exp 1) base)
        ((eqnumber? base 1) 1)
        ((and (number? base) (number? exp)) (expt base exp))
        (else (list '** base exp))))
(define (sum? x)
  (and (pair? x) (eq? (car x) '+)))
(define (addend s) (cadr s))
(define (augend s)
  (accumulate make-sum 0 (cddr s)))
(define (product? x)
  (and (pair? x) (eq? (car x) '*)))
(define (multiplier p) (cadr p))
(define (multiplicand p)
 (accumulate make-product 1 (cddr p)))
(define (exponent? x)
  (and (pair? x) (eq? (car x) '**)))
(define (base e) (cadr e))
(define (exponent e) (caddr e))

(define (deriv exp var)
  (cond ((number? exp) 0)
        ((variable? exp)
         (if (same-variable? exp var) 1 0))
        ((sum? exp)
         (make-sum (deriv (addend exp) var)
                   (deriv (augend exp) var)))
        ((product? exp)
         (make-sum
           (make-product (multiplier exp)
                         (deriv (multiplicand exp) var))
           (make-product (deriv (multiplier exp) var)
                         (multiplicand exp))))
        ((exponent? exp)
         (make-product
           (make-product
             (exponent exp)
             (make-exponent (base exp)
                            (make-sum (exponent exp) -1)))
           (deriv (base exp) var)))
        (else (error "Unknown expression type -- DERIV" exp))))
